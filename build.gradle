plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'io.github.parsingk'
archivesBaseName = "mysql-diff-copy"
version = '1.0.3'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

jar {
	enabled = false
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

repositories {
	mavenCentral()
}

publishing {
	publications {
		mavenJava(MavenPublication){

			groupId = 'io.github.parsingk'
			artifactId = 'mysql-diff-copy'
			version = '1.0.3'
			from components.java

			artifact javadocJar
			artifact sourcesJar

			pom {
				name = 'mysql-diff-copy'
				description = 'Mysql Tables, Columns, Data, Procedures, Functions Diff & Copy'
				url = 'https://github.com/parsingk/mysql-diff-copy'
				inceptionYear = '2022'

				licenses {
					license {
						name = 'Apache License 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
					}
				}
				developers {
					developer {
						id = 'parsingk'
						name = 'Jongpil Park'
						email = 'whdvlf2006@gmail.com'
					}
				}
				scm {
					connection='scm:git:git:github.com/parsingk/mysql-diff-copy.git'
					developerConnection='scm:git:https://github.com/parsingk/mysql-diff-copy.git'
					url='https://github.com/parsingk/mysql-diff-copy'
				}
			}
		}
	}
	repositories {
		maven {
			name = "ossrh"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}
		}
	}
}

ext."signing.keyId" = System.getenv('SIGN_KEY_ID')
ext."signing.password" = System.getenv("SIGN_KEY_PASSWORD")
ext."signing.secretKeyRingFile" = System.getenv("SIGN_KEY_FILE")

signing {
	sign configurations.archives
	sign publishing.publications.mavenJava
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'

	implementation group: 'com.googlecode.json-simple', name: 'json-simple', version:'1.1.1'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
}
